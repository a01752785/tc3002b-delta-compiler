comments = r'[/][/].*|[/][*](.|\n)*?[*][/]'
program = statement* expression EOF
statement = declaration / assignment / if / while / dowhile
declaration = 'var' decl_variable (',' decl_variable)* ';'
decl_variable = identifier
assignment = lhs_variable '=' expression ';'
if = 'if' expression block ('else if' expression block)* (r'else' block)?
while = 'while' expression block
dowhile = 'do' block 'while' expression ';'
block = '{' statement* '}' 
lhs_variable = identifier
identifier = r'[a-zA-Z]\w*'
expression = andexpression ('||' andexpression)*
andexpression = comparison ('&&' comparison)*
comparison = additive ((r'(==)' / r'(!=)' / r'(>=)' / r'(>)' / r'(<=)' / r'(<)') additive)*
additive = multiplicative (('+' / '-') multiplicative)*
multiplicative = unary (('*' / '/' / '%') unary)*
unary = ('+' / '-' / '!')* primary
primary = integer / boolean / parenthesis / rhs_variable
integer = decimal / binary / octal / hexadecimal
rhs_variable = identifier
decimal = r'\d+'
binary = r'#b[0|1]+'
octal = r'#o[0-7]+'
hexadecimal = r'#x[0-9a-fA-F]+'
boolean = 'true' / 'false'
parenthesis = '(' expression ')'